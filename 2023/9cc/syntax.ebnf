program = fndef*;

fndef = ident "(" params? ")" "{" stmt* "}";

params = ident
       | ident "," params;

stmt = expr ";"
     | "if" "(" expr ")" stmt ("else" stmt)?
     | "return" expr ";"
     | "{" stmt* "}"
     ;

expr = assign;

assign = equality ("=" assign)?;

equality = relational ("==" relational | "!=" relational)*;

relational = add ("<=" add | ">=" add | "<" add | ">" add)*;

add = mul ("+" mul | "-" mul)*;

mul = unary ("*" unary | "/" unary)*;

unary = ("+" | "-")? primary;

primary = num
        | ident ( "("  ")" )?
        | "&" unary
        | "*" unary
        | "(" expr ")";
